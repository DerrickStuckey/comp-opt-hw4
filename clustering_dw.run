## HW 4 (2c) Clustering implementation

reset;

# load model and data file
model clustering.mod;
data Clustering_1.dat;
#data Clustering_2.dat;

# setup iteration counter and max iterations
param cur_iter default 0;
param maxiter := 300;

let nCOL := 0;

#specify solver
option solver gurobi;
option gurobi_options 'outlev=1';

## Phase 1 RMP ##
problem MasterI: lambda, Artf_V, Artf_C, Art_Weights, Fill, Clusters;
   option relax_integrality 1;

## Phase 1 sub-problem (Artificial reduced cost calculation) ##
problem PricingI: y, w, Art_Reduced_Cost, Feas1, Feas2, Feas3, Capacity, Capacity1, Males, Females;
   option relax_integrality 0;

## Phase 2 RMP ##
problem RMP: lambda, WeightsInClusters, Fill, Clusters;
   option relax_integrality 1;

## Phase 2 sub-problem ##
problem Pricing: y, w, Red_Cost, Feas1, Feas2, Feas3, Capacity, Capacity1, Males, Females;
   option relax_integrality 0;

## initialize w/ Artificial basis ##
printf "Phase 1\n";
solve MasterI;
let {i in V} price[i] := Fill[i].dual;
let priceclusters:= Clusters.dual;

### Generate feasible non-Artificial basis ###
repeat {
	
	let cur_iter := cur_iter + 1;

	#some debugging statements
	printf "\nNew Phase I Iteration\n";
	display cur_iter;
	display nCOL;
	
	solve PricingI;
	
	# try 0.001 if needed
	if Art_Reduced_Cost > 0.00001 then {
		let nCOL := nCOL + 1;
		let {i in V} nbr[i,nCOL] := y[i];
		let weights[nCOL]:=sum{(i,j) in E} w[i,j]*eweights[i,j];
		let lambda[nCOL].relax := 1;
	}
	
	solve MasterI;
	
	if sum{i in V} Artf_V[i] + Artf_C <= 0.0001 then break;
	else {
		let {i in V} price[i] := Fill[i].dual;
		let priceclusters := Clusters.dual;
	}

};

print "Feasible non-Artificial basis found";
printf "Number of columns used for Phase I:\n";
display nCOL;

# iterate again, checking Red_Cost instead
# when close (enough) to 0, done
let cur_iter := 0;

### Iterate over columns until reduced cost approaches 0 ###
repeat {

	#some debugging statements
	printf "\nNew Phase II Iteration";
	display cur_iter;
	display nCOL;

	if cur_iter == maxiter then break;
	
	let cur_iter := cur_iter + 1;
	
	## Solve RMP ##
	solve RMP;
	
	print "(Phase 2) RMP solution:";
	display WeightsInClusters;
	display lambda;
	
	## Retrieve Optimal Dual Variables ##
	let {i in V} price[i] := Fill[i].dual;
	let priceclusters := Clusters.dual;

	#show problem dimensionality
	option presolve 0;
	option show_stats 1;
	
	## Solve Pricing Problem ##
	print "Solving sub-problem";
	solve Pricing;
	
	#print current reduced cost
	print "Current MLP Reduced Cost:";
	display Red_Cost;
	
	## if reduced cost is not close to 0, try a new column
	# assignment specifies 0.001 but first tested w/ 0.01
	if Red_Cost > 0.01 then {
	
		## Generate a column
		let nCOL := nCOL + 1;
		let {i in V} nbr[i, nCOL] := y[i];
		let weights[nCOL] := sum {(i, j) in E} w[i, j] * eweights[i, j];
		let lambda[nCOL].relax := 1;
	}
	# if reduced cost close enough to 0, we're done
	else {
		break
	}
};

# print final stats after global solution found
print "Optimal Solution for Master Problem Found";

printf "Number of columns used after Phase II:\n";
display nCOL;

display lambda;

display WeightsInClusters;

display _total_solve_elapsed_time;